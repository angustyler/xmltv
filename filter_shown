#!/usr/bin/perl -w
# 
# filter_shown
# 
# Filter TV listings, removing programmes that have already finished.
# 
# Usage: filter_shown [filename]
# 
# Reads from filename, or stdin if no filename given, and writes
# filtered XML to stdout.
# 
# -- Ed Avis, epa98@doc.ic.ac.uk, 2000-06-27
# 

use strict;
use XML::DOM;
use Date::Manip;

die "usage: $0 [filename]" if @ARGV >= 2;
@ARGV = ('-') if not @ARGV;

# TODO: Maybe a command line option to give the date?
my $now = ParseDate('today');

my $p = new XML::DOM::Parser;
my $doc = $p->parsefile($ARGV[0]);
my $nodes = $doc->getElementsByTagName('programme');
my $n = $nodes->getLength();

for (my $i = 0; $i < $n; $i++) {
    my $node = $nodes->item($i);
    my $stop = $node->getAttribute('stop');
    next if not defined $stop or $stop eq '';
    my $stop_d = ParseDate($stop);
    if (Date_Cmp($now, $stop_d) > 0) {
	# Programme has already finished, remove it.
	$doc->getDocumentElement()->removeChild($node);
    }
}

$doc->printToFileHandle(\*STDOUT);
