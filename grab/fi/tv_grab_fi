#!/usr/bin/perl -w

=head1 NAME

tv_grab_fi - Grab TV listings for Finland.

=head1 SYNOPSIS

tv_grab_fi --help

tv_grab_fi [--config-file FILE] --configure

tv_grab_fi [--config-file FILE] [--output FILE] [--days N]
           [--offset N] [--quiet]

tv_grab_fi --list-channels

=head1 DESCRIPTION

Output TV listings for several channels available in Finland.
The data comes from www.katso.fi which is owned by one of the largest
companies providing TV listings in Finland.  The grabber relies on
parsing HTML so it might stop working at any time.

First run B<tv_grab_fi --configure> to choose, which channels you want
to download. Then running B<tv_grab_fi> with no arguments will output
listings in XML format to standard output.

B<--configure> Prompt for which channels,
and write the configuration file.

B<--config-file FILE> Set the name of the configuration file, the
default is B<~/.xmltv/tv_grab_fi.conf>.  This is the file written by
B<--configure> and read when grabbing.

B<--output FILE> write to FILE rather than standard output.

B<--days N> grab N days.  The default is ten.

B<--offset N> start N days in the future.  The default is to start
from today.

B<--quiet> suppress the progress messages normally written to standard
error.

B<--list-channels> write output giving <channel> elements for every
channel available (ignoring the config file), but no programmes.

=head1 SEE ALSO

L<xmltv(5)>.

=head1 AUTHOR

Matti Airas, mairas@iki.fi.  Based on tv_grab_sn by Stefan G:orling.

=head1 BUGS

The data source does not include full channels information and the
channels are identified by short names rather than the RFC2838 form
recommended by the XMLTV DTD.

I havenE<39>t even considered what happens with DST issues.

=cut

######################################################################
# initializations

use strict;
use Getopt::Long;
use Date::Manip;
use LWP::Simple;
use HTML::TreeBuilder;
use HTML::Entities; # parse entities
use IO::File;

use XMLTV;
use XMLTV::Memoize;
use XMLTV::Ask;
# Todo: perhaps we should internationalize messages and docs?
use XMLTV::Usage <<END
$0: get Finnish television listings in XMLTV format
To configure: $0 --configure [--config-file FILE]
To grab listings: $0 [--config-file FILE] [--output FILE] [--days N]
        [--offset N] [--quiet] [--fast]
To list channels: $0 --list-channels
END
  ;

# Use Term::ProgressBar if installed.
my $have_bar;
BEGIN {
    eval { require Term::ProgressBar };
    $have_bar = not $@;
}

# Attributes of the root element in output.
my $HEAD = { 'source-info-url'     => 'http://www.katso.fi/',
	     'source-data-url'     => "http://www.katso.fi/tvopas",
	     'generator-info-name' => 'XMLTV',
	     'generator-info-url'  => 'http://membled.com/work/apps/xmltv/',
	   };

# Whether zero-length programmes should be included in the output.
my $WRITE_ZERO_LENGTH = 0;

# default timezone (shouldn't be needed??)
#my $TZ="EET";
my $TZ="";

# default language
my $LANG="fi";

# Global channel data.
use vars '@ch_all';


######################################################################
# get options

# Get options, including undocumented --cache option.
XMLTV::Memoize::check_argv('get');
my ($opt_days, $opt_offset, $opt_help, $opt_output,
    $opt_configure, $opt_config_file, $opt_quiet, $opt_fast,
    $opt_list_channels);
$opt_days  = 10; # default
$opt_offset = 0; # default
$opt_quiet  = 0; # default
$opt_fast = 0; # default
GetOptions('days=i'        => \$opt_days,
	   'offset=i'      => \$opt_offset,
	   'help'          => \$opt_help,
	   'configure'     => \$opt_configure,
	   'config-file=s' => \$opt_config_file,
	   'output=s'      => \$opt_output,
	   'quiet'         => \$opt_quiet,
	   'fast'	   => \$opt_fast,
	   'list-channels' => \$opt_list_channels,
	  )
  or usage(0);
die 'number of days must not be negative'
  if (defined $opt_days && $opt_days < 0);
usage(1) if $opt_help;
$have_bar = 0 if $opt_quiet;
if ($opt_configure and $opt_list_channels) {
    warn 'cannot both configure and list channels, assuming --list-channels only';
    $opt_configure = 0;
}

# File that stores which channels to download.
my $CONFIG_FILE = $opt_config_file;
if (not defined $CONFIG_FILE) {
    my $old;
    if (defined($ENV{HOME})) {
	my $conf_dir = "$ENV{HOME}/.xmltv";
	(-d $conf_dir) or mkdir($conf_dir, 0777)
	  or die "cannot mkdir $conf_dir: $!";
	$CONFIG_FILE = "$conf_dir/tv_grab_fi.conf";
	$old = "$conf_dir/tv_grab_fi";
    }
    else {
	# $HOME not set, use current directory.
	$CONFIG_FILE = 'tv_grab_fi.conf';
	$old = 'tv_grab_fi';
    }

    if (-f $old and not -e $CONFIG_FILE) {
	warn "migrating config file $old -> $CONFIG_FILE\n";
	rename($old, $CONFIG_FILE)
	  or die "cannot rename $old to $CONFIG_FILE: $!";
    }
}

# FIXME commonize this configuration stuff within the XMLTV project.
if ($opt_configure) {
    if (-e $CONFIG_FILE) {
	print <<END
The configuration file $CONFIG_FILE already exists.  There is
currently no support for altering an existing configuration, you have
to reconfigure from scratch.

END
  ;
	if (not askBooleanQuestion
	    ('Do you wish to overwrite the old configuration?', 0)) {
	    print "Exiting.\n";
	    exit 0;
	}
    }
}
else {
    if (-e $CONFIG_FILE) {
	print STDERR "using config file $CONFIG_FILE\n" unless $opt_quiet;
    }
    else {
	die "config file $CONFIG_FILE does not exist, run me with --configure\n";
    }
}

# Whatever we are doing, we need the channels data.
my %channels = get_channels(); # sets @ch_all
my @channels;

######################################################################
# write configuration

if ($opt_configure) {
    open(CONF, ">$CONFIG_FILE") or die "cannot write to $CONFIG_FILE: $!";
    my $answer;
    my $answer_all;

    foreach my $ch_did (sort(keys %channels)) {
	my $ch_name = $channels{$ch_did};
	if ($answer_all) {
	    print($answer eq 'yes' ? 'adding' : 'ignoring',
		  " new channel $ch_name\n");
	}
	else {
	    $answer=askQuestion("add channel $ch_name?", 'yes',
				'yes', 'no', 'all', 'none');
	    if ($answer eq 'all') {
		$answer_all=1;
		$answer='yes';
	    }
	    elsif ($answer eq 'none') {
		$answer_all=1;
		$answer='no';
	    }
	}
	
	if ($answer eq 'yes') {
	    print CONF "channel $ch_did $ch_name\n";
	}
	else {
	    print CONF "# channel $ch_did $ch_name\n";
	}
    }
    close CONF or warn "cannot close $CONFIG_FILE: $!";
    print "All done, run with no arguments to grab listings.\n";

    exit();
}

# Not configuration, we must be writing something, either full
# listings or just channels.
#
# Options to be used for XMLTV::Writer.
my %w_args;
if (defined $opt_output) {
    my $fh = new IO::File(">$opt_output");
    die "cannot write to $opt_output: $!" if not defined $fh;
    $w_args{OUTPUT} = $fh;
}
$w_args{encoding} = 'ISO-8859-1';
my $writer = new XMLTV::Writer(%w_args);
$writer->start($HEAD);

if ($opt_list_channels) {
    # Write channels mode.
    $writer->write_channel($_) foreach @ch_all;
    $writer->end();
    exit();
}

######################################################################
# We are producing full listings.  Read configuration.

open(CONF, $CONFIG_FILE) or die "cannot read $CONFIG_FILE: $!\n";
while (<CONF>) {
    s/\#.*//; s/^\s+//; s/\s+$//;
    next if not length;
    if (/^channel:?\s+(\S+)\s+([^\#]+)/) {
	my $ch_did = $1;
	my $ch_name = $2;
	$ch_name =~ s/\s*$//;
	push @channels, $ch_did;
	$channels{$ch_did} = $ch_name;
    }
    else {
	warn "$CONFIG_FILE:$.: bad line\n";
    }
}
close(CONF) or warn "could not close $CONFIG_FILE: $!\n";

######################################################################
# begin main program

# Assume the listings source uses EET (see BUGS above).
#Date_Init("TZ=$TZ");
my $now = DateCalc(ParseDate('now'), "$opt_offset days");
die "No channels specified, run me with --configure\n"
  if not keys %channels;
my @to_get;

# the order in which we fetch the channels matters
foreach my $ch_did (@channels) {
    my $ch_name=$channels{$ch_did};
    my $ch_xid="$ch_did.katso.fi";
    $writer->write_channel({ id => $ch_xid,
			     'display-name' => [ [ $ch_name ] ] });
    my $day=UnixDate($now,'%Q');
    for (my $i=0;$i<$opt_days;$i++) {
	push @to_get, [ $day, $ch_xid, $ch_did ];
	#for each day
	$day=nextday($day); die if not defined $day;
    }
}

# This progress bar is for both downloading and parsing.  Maybe
# they could be separate, as with tv_grab_uk.
#
my $bar = new Term::ProgressBar('getting listings', scalar @to_get)
  if $have_bar;
foreach (@to_get) {
    process_table($writer, $_->[0], $_->[1], $_->[2]);
    # be courteous to the servers
    sleep(rand(5)) unless $opt_fast;
    update $bar if $have_bar;
}
$writer->end();

######################################################################
# subroutine definitions

# Use Log::TraceMessages if installed.
BEGIN {
    eval { require Log::TraceMessages };
    if ($@) {
	*t = sub {};
	*d = sub { '' };
    }
    else {
	*t = \&Log::TraceMessages::t;
	*d = \&Log::TraceMessages::d;
	Log::TraceMessages::check_argv();
    }
}

####
# process_table: fetch a URL and process it
#
# arguments:
#    XMLTV::Writer object to write to
#    Date::Manip object giving the day to grab
#    xmltv id of channel
#    katso id of channel
sub process_table {
    my ($w, $date, $ch_xmltv_id, $ch_katso_id) = @_;

    my $today = UnixDate($date, '%Y-%m-%d');
    my $url = "http://www.katso.fi/tvopas/?pvm=$today&tv_kanava=$ch_katso_id";
    my $data=get($url);
    if (not defined $data) {
	die "could not fetch $url, aborting\n";
    }
    local $SIG{__WARN__} = sub {
	warn "$url: $_[0]";
    };

    # parse the page to a document object
    my $tree = HTML::TreeBuilder->new();
    $tree->parse($data);
    my @program_data = get_program_data($tree);
    my $bump_start_day=0;

    while (@program_data) {
	my $cur = shift @program_data;
	my $next = shift @program_data;
	unshift @program_data,$next if $next;
	$w->write_programme(make_programme_hash($date, $ch_xmltv_id, $ch_katso_id, $cur, $next));
	if (!$bump_start_day && bump_start_day($cur,$next)) {
	    $bump_start_day=1;
	    $date = UnixDate(DateCalc($date,"+ 1 day"),'%Q');
	}
    }
}

sub make_programme_hash {
    my ($date, $ch_xmltv_id, $ch_katso_id, $cur, $next) = @_;

    my %prog;

    $prog{channel}=$ch_xmltv_id;
    $prog{title}=[ [ $cur->{title}, $LANG ] ];

    my $start=ParseDate("$date $cur->{time} $TZ");
    my $stoptime=$next ? $next->{time} : undef;
    my $stop=get_stop_time($date,$start,$stoptime);
    $prog{start}=UnixDate($start,'%q %z');
    $prog{stop}=UnixDate($stop,'%q %z');

    # FIXME: parse description field further
    $prog{desc}=[ [ $cur->{desc}, $LANG ] ] if defined $cur->{desc};

    return \%prog;
}
sub bump_start_day {
    my ($cur,$next) = @_;
    if (!defined($next)) {
	return undef;
    }
    my $start = UnixDate($cur->{time},'%H:%M');
    my $stop = UnixDate($next->{time},'%H:%M');
    if (Date_Cmp($start,$stop)>0) {
	return 1;
    } else {
	return 0;
    }
}
sub get_stop_time {
    my ($date, $start, $stop) = @_;
    my $today = UnixDate($date, '%Q');

    my $longstop;
    if (defined($stop)) {
	$longstop = ParseDate("$today $stop $TZ");
	my $cmp = Date_Cmp(UnixDate($start,'%H:%M'),
			   UnixDate($longstop,'%H:%M'));

	# test whether we passed midnight
	my $day = $cmp>0 ? nextday($today) : $today;
	return ParseDate("$day $stop $TZ");
    } else {
	# FIXME: should honor $WRITE_ZERO_LENGTH
	$longstop = DateCalc($start,"+ 3 hours");
	return ParseDate("$longstop");
    }
}

# program data is split as follows:
# - td with class=kanava-klo is the time
# - its sibling td with class=kanava-ohj(-nyt)? (two to the right)
#   is the program
# - second td on the next row is the description
sub get_program_data {
    my ($tree) = @_;
    my @data;

    my @time_elems = get_time_elems($tree);

    # Subroutines to call on each element.
    my %keys = (time => \&get_time_str,
		title => \&get_name_str,
		desc => \&get_desc_str,
	       );
    # Actually time and title are required, but we don't check that.
    foreach my $elem (@time_elems) {
	my %h;
	foreach my $k (keys %keys) {
	    my $sub = $keys{$k};
	    my $v = $sub->($elem);
	    if (defined $v) {
		$h{$k} = $v;
	    }
	}
	push @data, \%h;
    }
    return @data;
}
sub get_time_elems {
    my ($tree) = @_;
    my @time_elems = $tree->look_down("_tag"=>"td",
				      "class"=>"kanava-klo");
    return @time_elems;
}
sub get_time_str {
    my ($time_elem) = @_;
    return get_content($time_elem->content_list());
}
sub get_name_str {
    my ($time_elem) = @_;
    return get_content(navigate($time_elem,("right","right")));
}
sub get_desc_str {
    my ($time_elem) = @_;

    my @next = navigate($time_elem,("parent","right"));
    if (@next) {
      return get_content($next[1]->content_list());
    } else {
      return undef;
    }
}
sub get_content {
  my (@l) = @_;
  while (@l) {
    my $e = shift @l;
    return $e if !ref($e);
  }
  return undef;
}

sub navigate {
  my ($elem,@dir) = @_;

  if (!@dir) {
    return $elem->content_list();
  } else {
    my $d = shift @dir;
    my $e = $elem->$d();
    if (defined($e)) {
      return navigate($e,@dir);
    } else {
      return undef;
    }
  }
}

# get channel listing
sub get_channels {
    my $bar = new Term::ProgressBar('getting list of channels', 1)
      if $have_bar;
    my %channels;
    my $url="http://www.katso.fi/tvopas";
    my $local_data=get($url);
    die "could not get channel listing $url, aborting\n"
      if not defined $local_data;

    my $tree = HTML::TreeBuilder->new();
    $tree->parse($local_data);

    # all channel elements are specially formatted <a/> tags
    # with class=white

    my @white_elems = $tree->look_down("_tag"=>"a",
				       "class"=>"white");
    foreach my $elem (@white_elems) {
	my $href = $elem->attr('href');
	if ($href =~ m!^/tvopas/\?pvm=([\d\-]*?)&tv_kanava=(.*?)$!) {
	    my $channel_id=$2;
	    my @children=$elem->content_list;
	    if (scalar(@children) != 1 || ref($children[0])) {
		warn "Illegal channel name";
		next;
	    }
	    for ($children[0]) {
		s/^\s+//; s/\s+$//;
		$channels{$channel_id}=$_;
		push @ch_all, { 'display-name' => [ [ $_, $LANG ] ],
				'id' => $channel_id };
	    }
	}
    }
    die "no channels could be found" if not keys %channels;
    update $bar if $have_bar;
    return %channels;
}

# Bump a YYYYMMDD date by one.
sub nextday {
    my $d = shift;
    my $p = ParseDate($d);
    my $n = DateCalc($p, '+ 1 day');
    return UnixDate($n, '%Q');
}

